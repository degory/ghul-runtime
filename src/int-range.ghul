namespace Ghul is
    use Collections;

    trait Range[T] is
        from: T;
        to: T;
    si

    trait Resettable is
        @IL.name("Reset")
        reset();
    si
    
    struct INT_RANGE: Range[int], Iterable[int], Iterator[int], Resettable is
        iterator: Iterator[int] => self;

        from: int;
        to: int;

        current: int;
 
        init(from: int, to: int) is
            self.from = from;
            self.to = to;

            current = from - 1;
        si

        move_next() -> bool is
            current = current + 1;
            return current < to;
        si
        
        reset() is
            current = from - 1;
        si

        to_string() -> string is
            let result = new System.Text.StringBuilder();
            result
                .append(from)
                .append("..")
                .append(to);

            if current != from -1 then
                result
                    .append(" @ ")
                    .append(current);
            fi
            
            return result.to_string();
        si
    si

    struct INT_RANGE_INCLUSIVE: Range[int], Iterable[int], Iterator[int], Resettable is
        iterator: Iterator[int] => self;

        from: int;
        to: int;

        current: int;

        init(from: int, to: int) is
            self.from = from;
            self.to = to;

            current = from - 1;
        si

        move_next() -> bool is
            current = current + 1;
            return current <= to;
        si        

        reset() is
            current = from - 1;
        si
        
        to_string() -> string is
            let result = new System.Text.StringBuilder();
            result
                .append(from)
                .append("::")
                .append(to);

            if current != from -1 then
                result
                    .append(" @ ")
                    .append(current);
            fi
            
            return result.to_string();
        si
    si
si
